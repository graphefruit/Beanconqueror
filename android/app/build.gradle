apply plugin: 'com.android.application'

android {
    namespace "com.beanconqueror"
    compileSdk rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.beanconqueror.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 70501
        versionName "7.5.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    // Custom dependencies for custom native code
    implementation "androidx.documentfile:documentfile:1.0.1"

    // Default config from Capacitor
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')

    constraints {
      // Work around https://github.com/ionic-team/capacitor-barcode-scanner/pull/40
      // and https://github.com/ionic-team/capacitor-barcode-scanner/issues/41
      // to make barcode scanner work. Remove this once the upstream issues are resolved.
      implementation('androidx.camera:camera-camera2:1.4.0') {
        because 'capacitor-barcode-scanner pulls in the wrong version. See https://github.com/ionic-team/capacitor-barcode-scanner/pull/40 and https://github.com/ionic-team/capacitor-barcode-scanner/issues/41'
      }
      implementation('androidx.camera:camera-lifecycle:1.4.0') {
        because 'capacitor-barcode-scanner pulls in the wrong version. See https://github.com/ionic-team/capacitor-barcode-scanner/pull/40 and https://github.com/ionic-team/capacitor-barcode-scanner/issues/41'
      }
      implementation('androidx.camera:camera-view:1.4.0') {
        because 'capacitor-barcode-scanner pulls in the wrong version. See https://github.com/ionic-team/capacitor-barcode-scanner/pull/40 and https://github.com/ionic-team/capacitor-barcode-scanner/issues/41'
      }
    }
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
